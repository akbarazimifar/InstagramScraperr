import java.time.LocalDateTime

plugins {
    id 'java'
    id 'net.nemerosa.versioning' version '2.6.1'
}

group 'fe.igscraper'
version = versioning.info.full + "-" + (versioning.info.tag == null ? "dev_build" : versioning.info.tag)


def mainClassName = "fe.igscraper.Main"
def gitlabProjectId = "14044471"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.28.0'
    compile group: 'com.gitlab.grrfe', name: 'Logger', version: '1.0.0'
    compile group: 'com.gitlab.grrfe', name: 'Request', version: '1.0.0'
    compile group: 'com.gitlab.grrfe', name: 'BinaryVersion', version: '1.0.0'
    compile group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'
    compile group: 'com.github.zafarkhaja', name: 'java-semver', version: '0.9.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    from configurations.compile.collect {
        it.isDirectory() ? it : zipTree(it).matching {
            exclude {
                it.path.contains("META-INF")
            }
        }
    }

    manifest {
        attributes("Main-Class": mainClassName,
                   "Built-At": LocalDateTime.now(),
                   "Commit": versioning.info.full,
                   "Version": versioning.info.tag == null ? "dev_build" : versioning.info.tag,
                   "Gitlab-Project-Id": gitlabProjectId
        )
    }
}

